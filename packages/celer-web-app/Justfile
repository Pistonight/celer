WASM_DIR := "src/data/libs/libceler/wasm/"

install:
    npm install

ci:
    npm ci

lint:
    pylint scripts/validateimport.py
    python3 scripts/validateimport.py src
    npm run lint

lint-fix:
    npm run lint -- --fix

build: wasm
    wget -q -O {{WASM_DIR}}binaryen.tar.gz https://github.com/WebAssembly/binaryen/releases/download/version_105/binaryen-version_105-x86_64-linux.tar.gz
    tar -xzvf {{WASM_DIR}}binaryen.tar.gz -C {{WASM_DIR}}
    mv -f {{WASM_DIR}}celer_bg.wasm {{WASM_DIR}}celer_bg_unoptimized.wasm
    {{WASM_DIR}}binaryen-version_105/bin/wasm-opt {{WASM_DIR}}celer_bg_unoptimized.wasm -O3 -o {{WASM_DIR}}celer_bg.wasm
    npm run build

# Build wasm from celer lib and copy the file over
wasm:
    @just ../celer-lib/build
    rm -rf {{WASM_DIR}}
    cp -R ../celer-lib/pkg {{WASM_DIR}}


watch:
    npm run start

test:
    npm run test -- --watchAll=false

release: build
    mkdir -p ../../release
    cp -R -T build ../../release/celer-web-app

clean:
    rm -rf build
    rm -rf src/data/wasmlib

nuke: clean
    rm -rf node_modules
